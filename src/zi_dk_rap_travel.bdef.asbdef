managed;
with draft;
//strict ( 2 );

define behavior for ZI_DK_RAP_TRAVEL alias Travel
implementation in class zbp_i_dk_rap_travel unique
persistent table zdk_rap_atrav
draft table zdk_rap_dtrav
lock master total etag LocalLastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt


{
  create;
  update;
  delete;
  association _Booking { create; with draft; }

  // to auto manage the numbering
  field ( numbering : managed, readonly ) TravelUuid;

  // define readonly fields
  field ( readonly ) TravelId, TotalPrice, OverallStatus;
  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy, LocalLastChangedAt;
  // mandatory fields
  field ( mandatory ) AgencyId, CustomerId;
  //define action
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  internal action reCalculateTotalPrice;

  // add determinations
  determination setInitialStatus on modify { create; }
  determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
  determination calculateTravelID on save { create; }

  // add validations
  validation validateAgency on save { field AgencyId; create; }
  validation validateCustomer on save { field CustomerId; create; }
  validation validateDates on save { field BeginDate, EndDate; create; }

  draft determine action Prepare
  {
    validation validateAgency;
    validation validateCustomer;
    validation validateDates;
  }

  mapping for zdk_rap_atrav
    {
      TravelUuid         = travel_uuid;
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for ZI_DK_RAP_BOOKING alias Booking
implementation in class zbp_i_dk_rap_booking unique
persistent table zdk_rap_abook
draft table zdk_rap_dbook
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) TravelUuid;
  field ( numbering : managed, readonly ) BookingUuid;
  //read only fields
  field ( readonly ) BookingId;
  field ( readonly ) CreatedBy, LastChangedBy, LocalLastChangedAt;

  //add determination
  determination calculateBookingID on modify { create; }
  determination calculateTotalPrice on modify { field FlightPrice, CurrencyCode; }

  association _Travel { with draft; }
  mapping for zdk_rap_abook
    {
      BookingUuid        = booking_uuid;
      TravelUuid         = travel_uuid;
      BookingId          = booking_id;
      BookingDate        = booking_date;
      CustomerId         = customer_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      CreatedBy          = created_by;
      LastChangedBy      = last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
    }
}